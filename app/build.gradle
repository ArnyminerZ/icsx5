/*
 * Copyright (c) Ricki Hirner (bitfire web engineering).
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the GNU Public License v3.0
 * which accompanies this distribution, and is available at
 * http://www.gnu.org/licenses/gpl.html
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "at.bitfire.icsdroid"
        minSdkVersion 19
        targetSdkVersion 29
        multiDexEnabled true    // >64k methods for Android 4.4

        versionCode 43
        versionName "1.8.7"

        setProperty "archivesBaseName", "icsx5-" + getVersionCode() + "-" + getVersionName()

        buildConfigField "String", "okhttpVersion", "\"${versions.okhttp}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding.enabled = true

    flavorDimensions "distribution"
    productFlavors {
        standard {
        }
        gplay {
        }
    }

    packagingOptions {
        exclude 'LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    lintOptions {
        disable 'ExtraTranslation', 'MissingTranslation'
        disable 'InvalidPackage'
        disable 'OnClick'
    }
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"

    implementation project(':cert4android')
    implementation project(':ical4android')

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    implementation 'androidx.work:work-runtime-ktx:2.2.0'
    implementation 'com.google.android.material:material:1.0.0'

    implementation 'com.jaredrummler:colorpicker:1.1.0'
    implementation 'com.mikepenz:aboutlibraries:6.2.3'
    implementation "com.squareup.okhttp3:okhttp:${versions.okhttp}"
    implementation 'commons-io:commons-io:2.6'
}
